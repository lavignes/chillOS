pkg UInt;

use Ascii;
use U64;
use Err;

pub let MAX: UInt = 0xFFFF_FFFF_FFFF_FFFFu;
pub let MIN: UInt = 0x0000_0000_0000_0000u;
pub let BITS: UInt  = sizeof UInt * 8;
pub let EMPTY: []UInt = 0 as &UInt ~ 0;
pub let EMPTY_MUT: []mut UInt = 0 as &mut UInt ~ 0;

pub fn min(lhs: UInt, rhs: UInt) -> UInt {
    if lhs < rhs {
        return lhs;
    }
    return rhs;
}

pub fn parse(base: UInt, str: []U8) -> !UInt {
    let cutoff = MAX / base;
    let cutlim = MAX % base;
    let mut i = 0u;

    let mut val = 0u;
    for i < lengthof str {
        let mut c = str[i] as UInt;

        if Ascii::is_digit(c) {
            c -= '0';
        } else if Ascii::is_alpha(c) {
            if Ascii::is_upper(c) {
                c -= 'A' - 10;
            } else {
                c -= 'a' - 10;
            }
        } else {
            return Err::MALFORMED_INPUT err;
        }

        if c >= base {
            return Err::MALFORMED_INPUT err;
        }

        if (val > cutoff) || ((val == cutoff) && (c > cutlim)) {
            return Err::OUT_OF_RANGE err;
        }

        val *= base;
        val += c;
        i += 1;
    }
    return val ok;
}
