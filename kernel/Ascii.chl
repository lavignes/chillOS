pkg Ascii;

use UInt;

pub let ERR_NOT_FOUND: Int = 0;

pub fn compare(lhs: []U8, rhs: []U8): Int {
    let length = lengthof lhs;
    if (length != lengthof rhs) || (length == 0) {
        return -1;
    }
    let mut i = 0u;
    let mut cl: U8;
    let mut cr: U8;
    for i < length {
        cl = lhs[i];
        cr = rhs[i];
        if cl != cr {
            return cl - cr;
        }
        i += 1;
    }
    return 0;
}

pub fn find(str: []U8, substr: []U8): !UInt {
    if lengthof substr == 0 {
        return ERR_NOT_FOUND err;
    }
    let mut i = 0u;
    for::outer i < lengthof str {
        if str[i] != substr[0] {
            i += 1;
            continue;
        }
        let mut j = 1u;
        for ((i + j) < lengthof str) && (j < lengthof substr) {
            if str[i + j] != substr[j] {
                i += 1;
                continue outer;
            }
            j += 1;
        }
        return i ok;
    }
    return ERR_NOT_FOUND err;
}

pub fn split(str: []U8, substr: []U8, consumer: fn([]U8): !Nil): !Nil {
    return split_ctx(str, substr, consumer as &mut U8, fn(ctx: &mut U8, split: []U8): !Nil {
        return (ctx as fn([]U8): !Nil)(split);
    });
}

pub fn split_ctx(str: []U8, substr: []U8, ctx: &mut U8, consumer: fn(&mut U8, []U8): !Nil): !Nil {
    let mut i = 0u;
    let mut view: []U8 = &str[i] ~ lengthof str - i;
    for i < lengthof str {
        try let pos: UInt = find(view, substr) else {
            return consumer(ctx, view);
        }
        try let _: Nil = consumer(ctx, &str[i] ~ pos);
        i += pos + lengthof substr;
        view = &str[i] ~ lengthof str - i;
    }
}

pub fn is_digit(val: U8): Bool {
    return (val >= '0') && (val <= '9');
}

pub fn is_alpha(val: U8): Bool {
    return is_upper(val) || is_lower(val);
}

pub fn is_upper(val: U8): Bool {
    return (val >= 'A') && (val <= 'Z');
}

pub fn is_lower(val: U8): Bool {
    return (val >= 'a') && (val <= 'z');
}

pub fn to_lower(val: U8): U8 {
    if is_upper(val) {
        return toggle_case(val);
    }
    return val;
}

pub fn to_upper(val: U8): U8 {
    if is_lower(val) {
        return toggle_case(val);
    }
    return val;
}

pub fn toggle_case(val: U8): U8 {
    return val ^ 32;
}
