// methods of reading Flattended Device Tree (FDT) or DeviceTree Blob (DTB)
// data fed in from the bootloader.
// see https://devicetree-specification.readthedocs.io
pkg Kfdt;

use U8;
use U32;
use UInt;
use Mem;
use Ascii;

pub type Iter = {
    name: &U8,         // address of asciiz name
    name_len: UInt,
    first_prop: &U32,  // address of first prop token (or 0)
    prop: &U32,        // address of current prop token (or 0)
    first_child: &U32, // address of first child begin node token (or 0)
    child: &U32,       // address of current child begin node token (or 0)
    addr_cells: UInt,  // width in U32s used to define bus address offsets of direct child nodes
    size_cells: UInt,  // width in U32s used to define bus address sizes of direct child nodes
};

pub type Tree = {
    structs_addr: &U32,
    strings_addr: &U8,
    readonly_mem_map_addr: &U8,
    root: Iter,
};

pub let ERR_BAD_MAGIC: Int = -1;
pub let ERR_BAD_VERSION: Int = -2;
pub let ERR_MALFORMED: Int = -3;

pub fn init_from_addr(tree: &mut Tree, addr: &U8): Int {
    let mut header: RawHeader;

    // all of the data in the fdt header is U32BE, we deserialize the header into LE
    U32::map(
        &mut header as &mut U8 as &mut U32, addr as &U32,
        sizeof RawHeader / sizeof U32,
        U32::endian_swap);

    if header.magic != 0xd00dfeed {
        return ERR_BAD_MAGIC;
    }
    if header.last_comp_version != 16 {
        return ERR_BAD_VERSION;
    }

    tree.structs_addr = (addr + (header.off_dt_struct as UInt)) as &U32;
    tree.strings_addr = (addr + (header.off_dt_strings as UInt)) as &U8;
    tree.readonly_mem_map_addr = addr + (header.off_mem_rsvmap as UInt);

    let mut tok: &U32 = tree.structs_addr;
    iter_init(tree, &mut tree.root, &mut tok);
    return 0;
}

//pub fn iter_at_path(tree: &Tree, path: &U8, path_len: UInt, iter: &mut Iter): Int {
//
//}

pub type Uart = {
    model_name: &U8,
    model_name_len: UInt,
};

pub fn find_uart(tree: &mut Tree, uart: &mut Uart): Int {
    // for now, we'll assume a "chosen" uart is defined
    for tree.root.child != 0 {
        let mut child: Iter;
        let result: Int = next_child(tree, &mut tree.root, &mut child);
        if result != 0 {
            return result;
        }
    }
    return 0;
}

fn rewind(iter: &mut Iter) {
    iter.prop = iter.first_prop;
    iter.child = iter.first_child;
}

type Prop = {
    name: &U8,      // address of asciiz name
    name_len: UInt,
    data: &U8,
    data_len: UInt,
};

fn next_prop(tree: &Tree, iter: &mut Iter, prop: &mut Prop): Int {
    if (iter.prop == 0) || (*iter.prop != TOK_PROP) {
        Mem::set(prop as &mut U8, sizeof Prop, 0);
        return 0;
    }
    let mut val: U32 = U32::endian_swap(*iter.prop);
    iter.prop += 1; // consume TOK_PROP token
    prop.data_len = U32::endian_swap(*iter.prop) as UInt;
    iter.prop += 1; // consume data size
    if find_string(tree, U32::endian_swap(*iter.prop), &mut prop.name, &mut prop.name_len) != 0 {
        return ERR_MALFORMED;
    }
    iter.prop += 1; // consume name offset
    prop.data = iter.prop as &U8;
    // skip prop data, align to next token, and skip TOK_NOPs
    iter.prop = U32::align_to_next((iter.prop as &U8) + prop.data_len);
    iter.prop = U32::find(iter.prop, UInt::MAX, is_not_nop);
    if iter.prop == 0 {
        return ERR_MALFORMED;
    }
    return 0;
}

// performs recursive inorder traversals to get to the next child
fn next_child(tree: &Tree, iter: &mut Iter, child: &mut Iter): Int {
    let mut tok: &U32 = iter.child;
    let result: Int = iter_init(tree, child, &mut tok);
    if result != 0 {
        return result;
    }
    // so now we are either at the end of the child node, or at a grandchild
    let mut val: U32 = U32::endian_swap(*tok);
    for val == TOK_BEGIN_NODE {
        // unfortunately, we must recurse down the entire subtree to skip to the next child
        let mut grandchild: Iter; // create an iter over the granchildren
        let result: Int = iter_init(tree, &mut grandchild, &mut tok);
        if result != 0 {
            return result;
        }
        // skip over all great-grandchildren
        for grandchild.child != 0 {
            let mut tmp: Iter;
            let result: Int = next_child(tree, &mut grandchild, &mut tmp);
            if result != 0 {
                return result;
            }
        }
        // see if we have another grandchild
        tok += 1; // consume assumed TOK_END_NODE
        val = U32::endian_swap(*tok);
    }

    // we *must* be at the end of the child
    if val != TOK_END_NODE {
        return ERR_MALFORMED;
    }

    tok += 1;
    val = U32::endian_swap(*tok);
    // we now either have a TOK_BEGIN_NODE of a child, or the TOK_END_NODE
    if val == TOK_BEGIN_NODE {
        iter.child = tok;
        return 0;
    }
    if val != TOK_END_NODE {
        return ERR_MALFORMED;
    }
    iter.child = 0; // zero to indicate we are all done
    return 0;
}

let TOK_BEGIN_NODE: U32 = 0x0000_0001;
let TOK_END_NODE: U32 = 0x0000_0002;
let TOK_PROP: U32 = 0x0000_0003;
let TOK_NOP: U32 = 0x0000_0004;
let TOK_END: U32 = 0x0000_0009;

// the names are always in asciiz so we use that format for comparisons
let TOK_PROP_ADDR_CELLS: [U8; 15] = "#address-cells\x00";
let TOK_PROP_SIZE_CELLS: [U8; 12] = "#size-cells\x00";

fn is_not_nop(val: U32): Bool {
    return U32::endian_swap(val) != TOK_NOP;
}

fn find_string(tree: &Tree, string_offset: U32, string: &mut &U8, string_len: &mut UInt): Int {
    *string = (tree.strings_addr + (string_offset as UInt)) as &U8;
    let string_null_byte_addr: &U8 = U8::find(*string, UInt::MAX, U8::is_zero);
    if string_null_byte_addr == 0 {
        return ERR_MALFORMED;
    }
    *string_len = string_null_byte_addr - *string;
    return 0;
}

// starting from a likely TOK_NOP/TOK_BEGIN_NODE token, walk forward and initialize an Iter
fn iter_init(tree: &Tree, iter: &mut Iter, tok: &mut &U32): Int {
    Mem::set(iter as &mut U8, sizeof Iter, 0);

    *tok = U32::find(*tok, UInt::MAX, is_not_nop);
    if (*tok == 0) || (U32::endian_swap(**tok) != TOK_BEGIN_NODE) {
        return ERR_MALFORMED;
    }

    // the node name is the next ptr after the TOK_BEGIN_NODE
    *tok += 1;
    iter.name = *tok as &U8;
    // following the name is possible padding bytes and the next token
    let name_null_byte_addr: &U8 = U8::find(iter.name, UInt::MAX, U8::is_zero);
    if name_null_byte_addr == 0 {
        return ERR_MALFORMED;
    }
    iter.name_len = name_null_byte_addr - iter.name;
    // add padding to align to next token
    *tok = U32::align_to_next(name_null_byte_addr + 1);

    *tok = U32::find(*tok, UInt::MAX, is_not_nop);
    if *tok == 0 {
        return ERR_MALFORMED;
    }
    // after this, we may have a TOK_PROP, the TOK_BEGIN_NODE of a child, or the TOK_END_NODE
    let mut val: U32 = U32::endian_swap(**tok);
    if val == TOK_PROP {
        iter.first_prop = *tok;
        iter.prop = *tok;
        // now we need to skip over the prop data and all subsequent props
        for val == TOK_PROP {
            *tok += 1; // consume TOK_PROP token
            let prop_data_size: UInt = U32::endian_swap(**tok) as UInt;
            *tok += 1; // consume data size
            let mut prop_name: &U8;
            let mut prop_name_len: UInt;
            if find_string(tree, U32::endian_swap(**tok), &mut prop_name, &mut prop_name_len) != 0 {
                return ERR_MALFORMED;
            }
            *tok += 1; // consume name offset

            // if the property is one of #address-cells or #size-cells read them now
            if Ascii::z_compare(&(TOK_PROP_ADDR_CELLS[0]), prop_name) == 0 {
                iter.addr_cells = U32::endian_swap(**tok);
            } else if Ascii::z_compare(&(TOK_PROP_SIZE_CELLS[0]), prop_name) == 0 {
                iter.size_cells = U32::endian_swap(**tok);
            }
            // skip prop data, align to next token, and skip TOK_NOPs
            *tok = U32::align_to_next((*tok as &U8) + prop_data_size);
            *tok = U32::find(*tok, UInt::MAX, is_not_nop);
            if tok == 0 {
                return ERR_MALFORMED;
            }
            val = U32::endian_swap(**tok);
        }
    }
    // we now either have a TOK_BEGIN_NODE of a child, or the TOK_END_NODE
    if val == TOK_BEGIN_NODE {
        iter.first_child = *tok;
        iter.child = *tok;
    } else if val != TOK_END_NODE {
        return ERR_MALFORMED;
    }
    return 0;
}

type RawHeader = {
    magic: U32,
    totalsize: U32,
    off_dt_struct: U32,
    off_dt_strings: U32,
    off_mem_rsvmap: U32,
    version: U32,
    last_comp_version: U32,
    boot_cpuid_phys: U32,
    size_dt_strings: U32,
    size_dt_struct: U32,
};

